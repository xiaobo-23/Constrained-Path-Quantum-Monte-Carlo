!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
! 09/25/2022
! ADD SINUSOIDAL SPIN PINNING FIELDS; USING PERIODIC BOUNDARY CONDITION (PBC)
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
! PROGRAM: Several subroutines used to calculate the Green's function and static observables for H_T Hamiltonian.  
! COMMENT: Static observables for the H_T Hamiltonian.  
! AUTHOR:  Yuan-Yao He
! DATE:    2020-02-27
! PURPOSE: Different subroutines are introduced as following:
!
!   InitGrFctMat --> Subroutine used to calculate the initial GreenF matrix for the propagation;
!   PhyMeaStatHT --> Subroutine used to measure the physical observables of the H_T Hamiltonian;
!   ObStaEnrgyHT --> Subroutine used to measure the energies and occupations for H_T Hamiltonian;
!   GetRnUpnDwHT --> Subroutine to calculate n_{i\up} and n_{i\dw} for H_T Hamiltonian.
!             
! END PROGRAM
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%



!$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
!____________________________________ Begin subroutine __________________________________________________________________
!________________________________________________________________________________________________________________________
	subroutine InitGrFctMat() 
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%     
! PROGRAM:  InitGrFctMat()
! TYPE:     subroutine
! PURPOSE:  This subroutine is used to calculate the initial Green's function matrix for H_T Hamiltonian before 
!                   propagation for every sweep.
! KEYWORDS: Calculate Green's function matrix for H_T Hamiltonian.
! AUTHOR:   Yuan-Yao He
! TIME:     2020-02-27
! DESCRIPTION: Here we calculate the GreenF matrix by the method, GreenF = (1+e^{-betaT*H_T})^{-1}.
!
! END PROGRAM
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%   
!______________________________________________________________________________________________________________	  
!_________________________________________ Modules used in this subroutine ____________________________________
!______________________________________________________________________________________________________________ 
		use RealPrecsn
      use CoreParamt
      use MPISetting
		implicit none
!______________________________________________________________________________________________________________	  
!______________________________ Temperory Quantities used in the calculations _________________________________
!______________________________________________________________________________________________________________		
      integer I1, I2, SpnInd
      real(rp) Rtp0, Rtp1, Rtp2
      real(rp), external :: FermiDiracFunct
      real(rp), allocatable :: TmpMatrix(:, :, :)
!______________________________________________________________________________________________________________	  
!__________________________ Calculate Green's function matrix for H_T Hamiltonian _____________________________
!______________________________________________________________________________________________________________
!**************************************************************************************************	  
!___________________ 0. Allocate the necessary matrices ___________________________________________
!************************************************************************************************** 
      allocate(TmpMatrix(NumNS, NumNS, NmSpn)); TmpMatrix = 0.0_rp
!**************************************************************************************************	  
!___________________ 1. Apply GrnFunct = (1 + e^{-\beta*H_T}) formula _____________________________
!____________________________ = U * (1+e^{-beta*\Lambda})^{-1} * U^+ ______________________________
!**************************************************************************************************
!________________________________________________________________________________________ 	  
!_________________ (0) Obtain TmpMatrix = (1+e^{-beta*\Lambda})^{-1} * U^+ ______________
!________________________________________________________________________________________
   !$OMP PARALLEL &
   !$OMP PRIVATE(I2, SpnInd, I1, Rtp0, Rtp1)
   !$OMP DO
      do I1 = 1, NumNS, +1
         do SpnInd = 1, NmSpn, +1
            Rtp0 = HT_EigValu(I1, SpnInd) + ChemP_BT - HubbU_UHF/2.0_rp
            Rtp1 = FermiDiracFunct(BetaT, -Rtp0)
            do I2 = 1, NumNS, +1
               TmpMatrix(I1, I2, SpnInd) = Rtp1 * HT_EigVect(I2, I1, SpnInd)
            enddo
         enddo
      enddo
   !$OMP END DO     
   !$OMP END PARALLEL
!________________________________________________________________________________________ 	  
!_________________ (1) Obtain GrnFunct = U * TmpMatrix __________________________________
!________________________________________________________________________________________
      GrnFunct(:, :, :, 0) = 0.0_rp
      call dMatPrd_NN_QMC(NumNS, NumNS, NumNS, 1.0_rp, HT_EigVect(1, 1, 1), TmpMatrix(1, 1, 1), &
         & 0.0_rp, GrnFunct(1, 1, 1, 0))
!**************************************************************************************************	  
!___________________ 2. Deallocate the used matrices ______________________________________________
!************************************************************************************************** 
      if(allocated(TmpMatrix)) deallocate(TmpMatrix)
      
   end subroutine InitGrFctMat
!________________________________________________________________________________________________________________________  
!____________________________________ End subroutine ____________________________________________________________________
!$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
   


!$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
!____________________________________ Begin subroutine __________________________________________________________________
!________________________________________________________________________________________________________________________
	subroutine PhyMeaStatHT() 
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%     
! PROGRAM:  PhyMeaStatHT
! TYPE:     subroutine
! PURPOSE:  This subroutine is used to calculate the initial Green's function matrix for H_T Hamiltonian before
!                   propagation for every sweep.
! KEYWORDS: Physical observables of H_T Hamiltonian. 
! AUTHOR:   Yuan-Yao He
! TIME:     2020-02-27
! DESCRIPTION: Measure physical observables of H_T Hamiltonian. 
!
! END PROGRAM
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%   
!______________________________________________________________________________________________________________	  
!_________________________________________ Modules used in this subroutine ____________________________________
!______________________________________________________________________________________________________________ 
		use RealPrecsn
      use CoreParamt
		implicit none
!______________________________________________________________________________________________________________	  
!______________________________ Temperory Quantities used in the calculations _________________________________
!______________________________________________________________________________________________________________		
      integer I1, I2, SpnInd
      real(rp), allocatable :: GreenF (:, :, :)
      real(rp), allocatable :: GreenFC(:, :, :)
!______________________________________________________________________________________________________________	  
!__________________________ Calculate static physical observables for H_T Hamiltonian _________________________
!______________________________________________________________________________________________________________
!**************************************************************************************************	  
!___________________ 0. Allocate the necessary matrices ___________________________________________
!************************************************************************************************** 
      allocate(GreenF (NumNS, NumNS, NmSpn)); GreenF  = 0.0_rp
      allocate(GreenFC(NumNS, NumNS, NmSpn)); GreenFC = 0.0_rp
!**************************************************************************************************	  
!___________________ 1. Calculate physical observables for H_T Hamiltonian ________________________
!**************************************************************************************************
!________________________________________________________________________________________ 	  
!_________________ (0) First obtain the GreenF and GreenFC matrices _____________________
!_____________________ GreenF  = <c_i c_j^+> = GrnFunct(:, :, 0) ________________________
!_____________________ GreenFC = <c_i^+ c_j> = I - GreenF^T _____________________________
!________________________________________________________________________________________
      call dMat_Copy_QMC(GrnFunct(1, 1, 1, 0), GreenF(1, 1, 1))
      do SpnInd = 1, NmSpn, +1
         do I2 = 1, NumNS, +1
            do I1 = 1, NumNS, +1
               GreenFC(I1, I2, SpnInd) = - GreenF(I2, I1, SpnInd)
            enddo
            GreenFC(I2, I2, SpnInd) = 1.0_rp + GreenFC(I2, I2, SpnInd)
         enddo
      enddo
!________________________________________________________________________________________ 	  
!_________________ (1) Measure the energies of all terms in H_T _________________________
!________________________________________________________________________________________
      call ObStaEnrgyHT(GreenF, GreenFC)
!________________________________________________________________________________________ 	  
!_________________ (2) Measure the spin density for the H_T Hamiltonian _________________
!________________________________________________________________________________________
      call GetRnUpnDwHT(GreenF, GreenFC)
!**************************************************************************************************	  
!___________________ 2. Deallocate the used matrices ______________________________________________
!************************************************************************************************** 
      if(allocated(GreenF )) deallocate(GreenF )
      if(allocated(GreenFC)) deallocate(GreenFC)
      
   end subroutine PhyMeaStatHT
!________________________________________________________________________________________________________________________  
!____________________________________ End subroutine ____________________________________________________________________
!$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$

   
   
!$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
!____________________________________ Begin subroutine __________________________________________________________________
!________________________________________________________________________________________________________________________
	subroutine ObStaEnrgyHT(GrF, GrFC) 
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%     
! PROGRAM:  ObStaEnrgyHT(GrF, GrFC) 
! TYPE:     subroutine
! PURPOSE:  This Subroutine is used to calculate the expectation energies for all the terms in the model Hamiltonian,
!               as well as the first-order derivatives over model parameters, and phase, sign.
! KEYWORDS: Calculate expectation energy.
! AUTHOR:   Yuan-Yao He
! TIME:     2020-02-27
! DESCRIPTION: Calculate the expectation energies for all the terms in the model Hamiltonian.
!
!     Input:  GrF   --> Green's Function matrice <c_i * c_j^+>;
!             GrFC  --> Green's Function matrice <c_i^+ * c_j>; 
!     
!     Output: (none).
!
! END PROGRAM
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%   
!______________________________________________________________________________________________________________	  
!_________________________________________ Modules used in this subroutine ____________________________________
!______________________________________________________________________________________________________________ 
		use RealPrecsn
		use CoreParamt
      use MPISetting
		implicit none
!______________________________________________________________________________________________________________	  
!_________________________________________ All Input and Output Quantities ____________________________________
!______________________________________________________________________________________________________________
      real(rp) GrF (NumNS, NumNS, NmSpn)
      real(rp) GrFC(NumNS, NumNS, NmSpn)
!______________________________________________________________________________________________________________	  
!______________________________ Temperory Quantities used in the calculations _________________________________
!______________________________________________________________________________________________________________
      integer I0, I1, I2, I3, I4, imj
      integer Ix, Iy, SiteParity
		real(rp) Rtp0, Rtp1, Rtp2, Rtp3, Rtp4
      real(rp) HmOvHopt2, HmOvHopt3, HmOvZmFld, HmOvPinSz, HmOvUHFmf
      real(rp) EgHopt1, EgHopt2, EgHopt3, EgZmFld, EgPinSz, EgDopCh, EgUHFmf, EgTotal
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      integer tmp, tmp1, tmp2, waveGrid
      real(rp) phaseFactor, EgSinusoidalPinSz, HmOvSinusoidalPinSz 
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      real(rp) EgnOccpUp, EgnOccpDw, EgnOccpTt, EgRDouOcc
      real(rp) SpinDensity(NumL1)
!______________________________________________________________________________________________________________	  
!________________________________________ Main calculations ___________________________________________________
!______________________________________________________________________________________________________________
!**************************************************************************************************	  
!___________________ 0. Measure the expectation energies for the model ____________________________
!**************************************************************************************************              
!________________________________________________________________________________________ 	  
!_________________ (0) Expectation Energy of NN hopping term --> spin-independent _______
!________________________________________________________________________________________
      EgHopt1 = 0.0_rp
      do I0 = 1, NumNS, +1
!____________________________________________________________________________ 	  
!________________ [0] The two NN lattice sites of I0 site ___________________
!____________________________________________________________________________         
			I1 = FNNBond(I0, 1)
         I2 = FNNBond(I0, 2)
!____________________________________________________________________________ 	  
!________________ [1] Spin-up part of NN hopping ____________________________
!____________________________________________________________________________
         Rtp1 = Hopt1Up * dble(FNNStBnd(I0, 1))
         Rtp2 = Hopt1Up * dble(FNNStBnd(I0, 2))
         EgHopt1 = EgHopt1 + ( GrFC(I0, I1, 1) + GrFC(I1, I0, 1) ) * Hopt1Up * dble(FNNStBnd(I0, 1))
         EgHopt1 = EgHopt1 + ( GrFC(I0, I2, 1) + GrFC(I2, I0, 1) ) * Hopt1Up * dble(FNNStBnd(I0, 2))
!____________________________________________________________________________ 	  
!________________ [2] Spin-down part of NN hopping __________________________
!____________________________________________________________________________
         Rtp1 = Hopt1Dn * dble(FNNStBnd(I0, 1))
         Rtp2 = Hopt1Dn * dble(FNNStBnd(I0, 2))
         EgHopt1 = EgHopt1 + ( GrFC(I0, I1, 2) + GrFC(I1, I0, 2) ) * Hopt1Dn * dble(FNNStBnd(I0, 1))
         EgHopt1 = EgHopt1 + ( GrFC(I0, I2, 2) + GrFC(I2, I0, 2) ) * Hopt1Dn * dble(FNNStBnd(I0, 2))
      enddo
      EgHopt1 = EgHopt1 / dble(NumNS)
!________________________________________________________________________________________ 	  
!_________________ (1) Expectation Energy of 2NN hopping term --> spin-independent ______
!________________________________________________________________________________________
      HmOvHopt2 = 0.0_rp
      EgHopt2   = 0.0_rp
!*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&
!________________ For uniform type of t2 hopping --> Fermi surface ________________
!*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&  
      do I0 = 1, NumNS, +1
!____________________________________________________________________________ 	  
!________________ [0] The two SNN lattice sites of I0 site __________________
!____________________________________________________________________________         
			I1 = SNNBond(I0, 1)
         I2 = SNNBond(I0, 2)
!____________________________________________________________________________	  
!________________ [1] Spin-up part of NNN hopping ___________________________
!____________________________________________________________________________
         Rtp1 = dble(SNNStBnd(I0, 1))
         Rtp2 = dble(SNNStBnd(I0, 2))
         HmOvHopt2 = HmOvHopt2 + ( GrFC(I0, I1, 1) + GrFC(I1, I0, 1) ) * Rtp1
         HmOvHopt2 = HmOvHopt2 + ( GrFC(I0, I2, 1) + GrFC(I2, I0, 1) ) * Rtp2
!____________________________________________________________________________ 	  
!________________ [2] Spin-down part of NNN hopping _________________________
!____________________________________________________________________________
         Rtp1 = dble(SNNStBnd(I0, 1))
         Rtp2 = dble(SNNStBnd(I0, 2))
         HmOvHopt2 = HmOvHopt2 + ( GrFC(I0, I1, 2) + GrFC(I1, I0, 2) ) * Rtp1
         HmOvHopt2 = HmOvHopt2 + ( GrFC(I0, I2, 2) + GrFC(I2, I0, 2) ) * Rtp2
      enddo
      HmOvHopt2 = HmOvHopt2 / dble(NumNS)
      if(abs(Hopt2) > rp_Eps) then
         EgHopt2 = HmOvHopt2 * Hopt2
      end if
!________________________________________________________________________________________ 	  
!_________________ (2) Expectation Energy of 3NN hopping term --> spin-independent ______
!________________________________________________________________________________________
      HmOvHopt3 = 0.0_rp
      EgHopt3   = 0.0_rp
      do I0 = 1, NumNS, +1
!____________________________________________________________________________ 	  
!________________ [0] Lattice sites of NN ___________________________________
!____________________________________________________________________________
         I1 = TNNBond(I0, 1)
         I2 = TNNBond(I0, 2)
!____________________________________________________________________________ 	  
!________________ [1] Spin-up part of NN hopping ____________________________
!____________________________________________________________________________
         HmOvHopt3 = HmOvHopt3 + ( GrFC(I0, I1, 1) + GrFC(I1, I0, 1) ) * dble(TNNStBnd(I0, 1))
         HmOvHopt3 = HmOvHopt3 + ( GrFC(I0, I2, 1) + GrFC(I2, I0, 1) ) * dble(TNNStBnd(I0, 2))
!____________________________________________________________________________ 	  
!________________ [2] Spin-down part of NN hopping __________________________
!____________________________________________________________________________
         HmOvHopt3 = HmOvHopt3 + ( GrFC(I0, I1, 2) + GrFC(I1, I0, 2) ) * dble(TNNStBnd(I0, 1))
         HmOvHopt3 = HmOvHopt3 + ( GrFC(I0, I2, 2) + GrFC(I2, I0, 2) ) * dble(TNNStBnd(I0, 2))
      enddo
      HmOvHopt3 = HmOvHopt3 / dble(NumNS)
      if(abs(Hopt3) > rp_Eps) then
         EgHopt3 = HmOvHopt3 * Hopt3
      end if
!________________________________________________________________________________________ 	  
!_________________ (3) Expectation Energy of the Zeeman field in z-direction ____________
!________________________________________________________________________________________
      HmOvZmFld = 0.0_rp
      EgZmFld   = 0.0_rp   
      do I0 = 1, NumNS, +1
         HmOvZmFld = HmOvZmFld + GrFC(I0, I0, 1) - GrFC(I0, I0, 2)
      enddo
      HmOvZmFld = HmOvZmFld / dble(NumNS)
      if(ZmFdz > rp_Eps) EgZmFld = HmOvZmFld * ZmFdz/2.0_rp
!________________________________________________________________________________________ 	  
!_________________ (4) Expectation Energy of pinning Sz term ____________________________
!____________________________ --> spin-dependent ________________________________________
!________________________________________________________________________________________
      HmOvPinSz = 0.0_rp
      EgPinSz   = 0.0_rp
      if(abs(PinSz) >= rp_Eps) then
!____________________________________________________________________________ 	  
!________________ [0] Pinning AFM Sz along one edge of ribbon _______________
!____________________________________________________________________________
         if(PinSzType == 0) then
            do Iy = 1, NumL2, +1
               SiteParity = (-1)**(mod(Iy, 2))
               Ix = 1
               I1 = (Iy-1)*NumL1 + Ix
               HmOvPinSz = HmOvPinSz + ( GrFC(I1, I1, 1) - GrFC(I1, I1, 2) ) * dble(SiteParity)
            enddo
!____________________________________________________________________________ 	  
!________________ [1] Pinning AFM Sz along two edges of ribbon ______________
!____________________________________________________________________________
         else if(PinSzType == 1) then
            do Iy = 1, NumL2, +1
               SiteParity = (-1)**(mod(Iy, 2))
            
               Ix = 1
               I1 = (Iy-1)*NumL1 + Ix
               HmOvPinSz = HmOvPinSz + ( GrFC(I1, I1, 1) - GrFC(I1, I1, 2) ) * dble(SiteParity)
            
               Ix = NumL1
               I2 = (Iy-1)*NumL1 + Ix
               HmOvPinSz = HmOvPinSz - ( GrFC(I2, I2, 1) - GrFC(I2, I2, 2) ) * dble(SiteParity)
            enddo
!____________________________________________________________________________ 	  
!________________ [2] Pinning AFM Sz at all lattice sites ___________________
!____________________________________________________________________________
         else if(PinSzType == 2) then
            do Ix = 1, NumL1, +1
               do Iy = 1, NumL2, +1
                  SiteParity = (-1)**(mod(Ix+Iy, 2))
                  I1 = (Iy-1)*NumL1 + Ix
                  HmOvPinSz = HmOvPinSz + ( GrFC(I1, I1, 1) - GrFC(I1, I1, 2) ) * dble(SiteParity)
               enddo
            enddo
         end if
         HmOvPinSz = HmOvPinSz / dble(NumNS)
         EgPinSz   = HmOvPinSz * PinSz/2.0_rp
      end if

!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
! Expectation Energy of the Sinusoidal Pinning Fields
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      HmOvSinusoidalPinSz = 0.0_rp
      EgSinusoidalPinSz = 0.0_rp

      if (ifSinusoidalPinning) then
         do tmp1 = 1, NumL1, +1 
            waveGrid = mod((tmp1 - 1), 1)
            do tmp2 = 1, NumL2, +1
               tmp = (tmp2 - 1) * NumL1 + tmp1
               phaseFactor = (-1.0_rp)**(tmp1 + tmp2)
               HmOvSinusoidalPinSz = HmOvSinusoidalPinSz &
                  & + phaseFactor * (GrFC(tmp, tmp, 1) - GrFC(tmp, tmp, 2)) * cos((2.0_rp * rp_pi / LambdaSz) * waveGrid + rp_pi) 
            end do
         end do
         HmOvSinusoidalPinSz = HmOvSinusoidalPinSz / dble(NumNS)
         EgSinusoidalPinSz = HmOvSinusoidalPinSz * SinusoidalPinSz/2.0_rp
      end if
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


!________________________________________________________________________________________ 	  
!_________________ (5) Expectation Energy of dopping chemical potential _________________
!____________________________ --> spin-independent ______________________________________
!________________________________________________________________________________________
      EgDopCh = 0.0_rp
      if(abs(ChemP) > rp_Eps) then
         do I0 = 1, NumNS, +1
            EgDopCh = EgDopCh + ( GrFC(I0, I0, 1) + GrFC(I0, I0, 2) )
         enddo
         EgDopCh = EgDopCh * ChemP
      end if
      EgDopCh = EgDopCh / dble(NumNS)
!________________________________________________________________________________________ 	  
!_________________ (6) Expectation Energy of UHF mean-field for HubbU term ______________
!________________________________________________________________________________________
      HmOvUHFmf = 0.0_rp
      do I0 = 1, NumNS, +1
         Rtp1 = (MagMoment(I0, 2)-0.50_rp)*GrFC(I0, I0, 1) + (MagMoment(I0, 1)-0.50_rp)*GrFC(I0, I0, 2)
         Rtp1 = Rtp1 - MagMoment(I0, 2) * MagMoment(I0, 1)
         HmOvUHFmf = HmOvUHFmf + Rtp1
      enddo
      HmOvUHFmf = HmOvUHFmf / dble(NumNS)
         
      EgUHFmf = 0.0_rp
      if(abs(HubbU_UHF) > rp_Eps) then
         EgUHFmf = HmOvUHFmf * HubbU_UHF
      end if 

!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
! Total Energy with Contribution from Sinusoidal Pinning Fields
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      EgTotal = EgHopt1 + EgHopt2 + EgHopt3 + EgZmFld + EgPinSz + EgDopCh + EgUHFmf + EgSinusoidalPinSz
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


!**************************************************************************************************	  
!___________________ 1. Obtain the electron occupations for up and dn _____________________________
!**************************************************************************************************
!________________________________________________________________________________________ 	  
!_________________ (0) The total electron occupations ___________________________________
!________________________________________________________________________________________
      EgnOccpUp = 0.0_rp; EgnOccpDw = 0.0_rp
      do I0 = 1, NumNS, +1
         EgnOccpUp = EgnOccpUp + GrFC(I0, I0, 1)
         EgnOccpDw = EgnOccpDw + GrFC(I0, I0, 2)
      enddo
      EgnOccpUp = EgnOccpUp / dble(NumNS)
      EgnOccpDw = EgnOccpDw / dble(NumNS)
      EgnOccpTt = EgnOccpUp + EgnOccpDw
!________________________________________________________________________________________ 	  
!_________________ (1) The double occupancy for H_T _____________________________________
!________________________________________________________________________________________      
      EgRDouOcc = 0.0_rp
      do I0 = 1, NumNS, +1
         EgRDouOcc = EgRDouOcc + GrFC(I0, I0, 1)*GrFC(I0, I0, 2)
      enddo
      EgRDouOcc = EgRDouOcc / dble(NumNS)
!________________________________________________________________________________________ 	  
!_________________ (2) The spin density for only one row of the lattice _________________
!________________________________________________________________________________________    
      I2 = 1
      do I1 = 1, NumL1, +1
         I0 = (I2-1)*NumL1 + I1
         SpinDensity(I0) = ( GrFC(I0, I0, 1) - GrFC(I0, I0, 2) ) / 2.0_rp
      enddo
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%	  
! Output the results of parameters and energies 
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      if(amyid == amstr) then
!________________________________________________________________________________________
!_________________ (0) The simulation parameters for H_T Hamiltonian ____________________
!_____________________ And the energies and the electron occupations ____________________
!________________________________________________________________________________________
         open( 289, file = "Output/00_HT_Hmlt_EngOccp.txt")
      
         write(289, "(es17.8, A, es17.8, A)", advance = "no") Hopt1Up, char(9), Hopt1Dn, char(9)
         write(289, "(es17.8, A, es17.8, A)", advance = "no") Hopt2, char(9), Hopt3, char(9)
         write(289, "(es17.8, A, es17.8, A)", advance = "no") ZmFdz, char(9), PinSz, char(9)
         write(289, "(es17.8, A, es17.8, A)", advance = "no") ChemP_BT, char(9), HubbU_UHF, char(9)
         write(289, "(es17.8, A, es17.8   )", advance = "no") SinusoidalPinSz, char(9), LambdaSz
         write(289, "()")
      
         write(289, "(es17.8, A, es17.8, A, es17.8, A)", advance = "no") EgHopt1, char(9), EgHopt2, char(9)
         write(289, "(es17.8, A, es17.8, A, es17.8, A)", advance = "no") EgHopt3, char(9), EgZmFld, char(9), &
            & EgPinSz, char(9)
         write(289, "(es17.8, A, es17.8, A, es17.8, A)", advance = "no") EgDopCh, char(9), EgUHFmf, char(9), &
            & EgSinusoidalPinSz, char(9)
         write(289, "(es17.8  )") EgTotal
         write(289, "()")

      
         write(289, "(es17.8, A, es17.8, A, es17.8   )", advance = "no") EgnOccpUp, char(9), EgnOccpDw, char(9), &
            & EgnOccpTt
         write(289, "()")
         write(289, "()")
      
         close(289)
!________________________________________________________________________________________ 	  
!_________________ (1) The results of spin densities ____________________________________
!________________________________________________________________________________________
         open( 301, file = "Output/00_HT_Hmlt_SpnDnst.txt")
         do I1 = 1, NumL1, +1
            write(301, "(I4, A, es25.16)") I1, char(9), SpinDensity(I1)
         enddo
         close(301)
      end if
      
   end subroutine ObStaEnrgyHT
!________________________________________________________________________________________________________________________  
!____________________________________ End subroutine ____________________________________________________________________
!$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$



!$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
!____________________________________ Begin subroutine __________________________________________________________________
!________________________________________________________________________________________________________________________
	subroutine GetRnUpnDwHT(GrF, GrFC) 
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%     
! PROGRAM:  GetRnUpnDwHT(GrF, GrFC)
! TYPE:     subroutine
! PURPOSE:  This subroutine is used to calculate n_{i\up} and n_{i\dw} for all lattice sites in H_T Hamiltonian.
! KEYWORDS: Get n_{i\up} and n_{i\dw} of H_T Hamiltonian.
! AUTHOR:   Yuan-Yao He
! TIME:     2020-02-27
! DESCRIPTION: In this subroutine, we simply calculate the GreenF matrix by the standard way as (1+VL*Dl*UL)^(-1).
!
!     First calculate the single-particle Green's Function matrix, and then calculate n_{i\up} and n_{i\dw}, 
!        and then take average of results from all processes and then output the results.
!
!     Input:  GrF   --> Green's Function matrice <c_i * c_j^+>;
!             GrFC  --> Green's Function matrice <c_i^+ * c_j>; 
!     
!     Output: (none).
!
! END PROGRAM
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%   
!______________________________________________________________________________________________________________	  
!_________________________________________ Modules used in this subroutine ____________________________________
!______________________________________________________________________________________________________________ 
		use RealPrecsn
      use CoreParamt
      use MPISetting
      implicit none
!______________________________________________________________________________________________________________	  
!_________________________________________ All Input and Output Quantities ____________________________________
!______________________________________________________________________________________________________________
      real(rp) GrF (NumNS, NumNS, NmSpn)
      real(rp) GrFC(NumNS, NumNS, NmSpn)
!______________________________________________________________________________________________________________	  
!______________________________ Temperory Quantities used in the calculations _________________________________
!______________________________________________________________________________________________________________		
      integer I0, I1, I2
      real(rp) RnUpnDwA(NumNS, 3)
!______________________________________________________________________________________________________________	  
!__________________ Main calculations of n_{i\up} and n_{i\dw} for the H_T Hamiltonian ________________________
!______________________________________________________________________________________________________________
!**************************************************************************************************	  
!___________________ 0. Calculate the electron densities for H_T Hamiltonian ______________________
!**************************************************************************************************
!________________________________________________________________________________________ 	  
!_________________ (0) Obtain the n_{i\up} and n_{i\dn} for all sites ___________________
!________________________________________________________________________________________
      RnUpnDwA = 0.0_rp
      do I0 = 1, NumNS, +1
         RnUpnDwA(I0, 1) = GrFC(I0, I0, 1)
         RnUpnDwA(I0, 2) = GrFC(I0, I0, 2)
         RnUpnDwA(I0, 3) = RnUpnDwA(I0, 1) + RnUpnDwA(I0, 2)
      enddo
!________________________________________________________________________________________ 	  
!_________________ (1) Output the n_{i\up} and n_{i\dn} for H_T Hamiltonian _____________
!________________________________________________________________________________________
      if(amyid == amstr) then
         open( 301, file = "Output/00_HT_Hmlt_RnUpnDw.txt", access = "append")
         do I2 = 1, NumL2, +1
            do I1 = 1, NumL1, +1
               I0 = (I2-1)*NumL1 + I1
               write(301, "(I6, A, I4, A, I4      )", advance = "no") I0, char(9), I1, char(9), I2
               write(301, "(A, es25.16, A, es25.16)", advance = "no") char(9), RnUpnDwA(I0, 1), char(9), &
                  & RnUpnDwA(I0, 2)
               write(301, "(A, es25.16, A, es25.16)", advance = "no") char(9), RnUpnDwA(I0, 3), char(9), &
                  & ( RnUpnDwA(I0, 1) - RnUpnDwA(I0, 2) )/2.0_rp
               write(301, "()")
            enddo
         enddo
         close(301)
      end if
      
   end subroutine GetRnUpnDwHT
!________________________________________________________________________________________________________________________  
!____________________________________ End subroutine ____________________________________________________________________
!$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$